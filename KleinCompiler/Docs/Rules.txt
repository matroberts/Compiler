
R1      <Program>             ::= <Def> <DefTail> MakeProgram                               
R2      <DefTail>             ::= <Def> <DefTail>
R3                              | e                                                         
R4      <Def>                 ::= <Identifier> MakeIdentifier ( <Formals> ) : <Type> <Body> MakeDefinition
R5      <Formals>             ::= e
R6                              | <NonEmptyFormals>
R7      <NonEmptyFormals>     ::= <Formal><FormalTail>                                      
R8      <FormalTail>          ::= , <Formal><FormalTail>
R9                              | e
R10     <Formal>              ::= <Identifier> MakeIdentifier : <Type> MakeFormal
R11     <Body>                ::= <Print> <Body>
R12                             | <Expr> MakeBody
R13     <Type>                ::= integer MakeIntegerType
R14                             | boolean MakeBooleanType
R15     <Expr>                ::= <Simple-Expr> <SimpleExprTail>                            
R16     <SimpleExprTail>      ::= < <Simple-Expr> <SimpleExprTail>
R17                             | = <Simple-Expr> <SimpleExprTail>
R18                             | e
R19     <Simple-Expr>         ::= <Term> <TermTail>                                         
R20     <TermTail>            ::= or <Term> <TermTail>
R21                             | + <Term> <TermTail>
R22                             | - <Term> <TermTail>
R23                             | e
R24     <Term>                ::= <Factor><FactorTail>                                      
R25     <FactorTail>          ::= and <Factor><FactorTail>
R26                             | * <Factor><FactorTail>
R27                             | / <Factor><FactorTail>
R28                             | e
R29     <Factor>              ::= if <Expr> then <Expr> else <Expr>
R30                             | not <Factor>
R31                             | <Func>
R32                             | <Literal>
R33                             | - <Factor>
R34                             | ( <Expr> )
R35    <Func>                 ::= <Identifier><FuncTail>
R36    <FuncTail>             ::= ( <Actuals> )
R37                             | e
R38     <Actuals>             ::= e
R39                             | <NonEmptyActuals>
R40     <NonEmptyActuals>     ::= <Expr><ActualsTail>                                       
R41     <ActualsTail>         ::= , <Expr><ActualsTail>
R42                             | e
R43     <Literal>             ::= <Number>       MakeIntegerLiteral
R44                             | <BooleanTrue>  MakeBooleanTrueLiteral
R45                             | <BooleanFalse> MakeBooleanFalseLiteral
R46     <Print>               ::= print ( <Expr> )                                                                         
